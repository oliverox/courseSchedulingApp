package org.coursescheduling.solver;
    dialect "java"

import java.util.ArrayList
import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;

import org.coursescheduling.domain.CourseSchedulingSolution;
import org.coursescheduling.domain.StudentEntity;
import org.coursescheduling.domain.CourseEntity;

global HardSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "place into course by subject requested"
    when
        $arr1 : ArrayList(size > 0)
            from collect(StudentEntity(
                locked == false,
                assignedCourse != null,
                requestedCourseId != assignedCourse.getCourseId()))
    then
        scoreHolder.addHardConstraintMatch(kcontext, -$arr1.size());
end

rule "place by academy"
    when
        $arr2 : ArrayList(size > 0)
            from collect(StudentEntity(
                locked == false,
                assignedCourse != null,
                requestedCourseId == assignedCourse.getCourseId(),
                assignedCourse.getAcademy() != "",
                nextYearAcademy != assignedCourse.getAcademy()))
        then
            scoreHolder.addSoftConstraintMatch(kcontext, -$arr2.size());
end

rule "don't exceed course max capacity"
	when
		$course : CourseEntity($capacity : capacity)
		$arr3 : ArrayList(size > $capacity)
            from collect(StudentEntity(
                locked == false,
                assignedCourse != null,
                assignedCourse.getId() == $course.getId(),
                requestedCourseId == assignedCourse.getCourseId(),
                assignedCourse.getIndex() == $course.getIndex()))
	then
		scoreHolder.addHardConstraintMatch(kcontext, $capacity - $arr3.size());
end

rule "don't place into courses at the same periods"
    when
        $student : StudentEntity(assignedCourse != null, requestedCourseId == assignedCourse.getCourseId())
        $arr4 : ArrayList(size > 0)
            from collect(StudentEntity(
                locked == false,
                id != $student.getId(),
                studentId == $student.getStudentId(),
                assignedCourse != null,
                assignedCourse.getMarkingPeriods() == $student.getAssignedCourse().getMarkingPeriods(),
                assignedCourse.periodMatch($student.getAssignedCourse())))
    then
        scoreHolder.addHardConstraintMatch(kcontext, -$arr4.size());
end


// rule "assign courses with higher priority"
//     when
//         $student1 : StudentEntity(assigned != null, requested == assigned.getSubject())
//         $student2 : StudentEntity(assigned != null, name == $student1.getName(), priority > $student1.getPriority(), requested != assigned.getSubject())
//     then
//         scoreHolder.addSoftConstraintMatch(kcontext, -1);
// end
